CMAKE_MINIMUM_REQUIRED(VERSION 3.0.2)

CMAKE_POLICY(SET CMP0042 NEW)

INCLUDE(ExternalProject)

SET(CROSS_TARGET $ENV{CROSS_TARGET})
SET(CROSS_TRIPLE $ENV{CROSS_TRIPLE})

IF(CROSS_TARGET STREQUAL win32-ia32 OR CROSS_TARGET STREQUAL win32-x64)
  SET(CMAKE_SYSTEM_NAME Windows)
  # https://cmake.org/Bug/view.php?id=9985
  UNSET(CMAKE_SHARED_LIBRARY_LINK_C_FLAGS)
  UNSET(CMAKE_SHARED_LIBRARY_LINK_CXX_FLAGS)
ELSEIF(CROSS_TARGET STREQUAL linux-ia32 OR CROSS_TARGET STREQUAL linux-x64)
  SET(CMAKE_SYSTEM_NAME Linux)
ELSEIF(CROSS_TARGET STREQUAL darwin-x64)
  SET(CMAKE_SYSTEM_NAME Darwin)
ELSE()
  MESSAGE(FATAL_ERROR)
ENDIF()

IF(NOT CROSS_TARGET STREQUAL darwin-x64)
  SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -static-libgcc -static-libstdc++")
ENDIF()

PROJECT(tagit VERSION 0.0.1)

CONFIGURE_FILE(
  "${PROJECT_SOURCE_DIR}/tagit_config.h.in"
  "${PROJECT_BINARY_DIR}/tagit_config.h"
)

# START zlib
SET(ZLIB_INSTALL_DIR "${PROJECT_BINARY_DIR}/zlib/install")

EXTERNALPROJECT_ADD(zlib
  PREFIX zlib
  GIT_REPOSITORY https://github.com/madler/zlib.git
  GIT_TAG v1.2.8
  CMAKE_ARGS
    "-DCMAKE_SYSTEM_NAME=${CMAKE_SYSTEM_NAME}"
    "-DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}"
    "-DCMAKE_INSTALL_PREFIX=${ZLIB_INSTALL_DIR}"
)

IF(CMAKE_SYSTEM_NAME STREQUAL Windows)
  SET(ZLIB_STATIC_LIBRARY_OUTPUT_NAME zlibstatic)
ELSE()
  SET(ZLIB_STATIC_LIBRARY_OUTPUT_NAME z)
ENDIF()

SET(ZLIB_STATIC_LIBRARY_NAME "${CMAKE_STATIC_LIBRARY_PREFIX}${ZLIB_STATIC_LIBRARY_OUTPUT_NAME}${CMAKE_STATIC_LIBRARY_SUFFIX}")
SET(ZLIB_STATIC_LIBRARY_LOCATION "${ZLIB_INSTALL_DIR}/lib/${ZLIB_STATIC_LIBRARY_NAME}")

ADD_LIBRARY(${ZLIB_STATIC_LIBRARY_OUTPUT_NAME} STATIC IMPORTED)

SET_TARGET_PROPERTIES(${ZLIB_STATIC_LIBRARY_OUTPUT_NAME} PROPERTIES IMPORTED_LOCATION "${ZLIB_STATIC_LIBRARY_LOCATION}")
# END zlib

# START taglib
# https://mail.kde.org/pipermail/taglib-devel/2015-August/002778.html
ADD_DEFINITIONS(-DTAGLIB_STATIC)

SET(TAGLIB_INSTALL_DIR "${PROJECT_BINARY_DIR}/taglib/install")

EXTERNALPROJECT_ADD(taglib
  PREFIX taglib
  GIT_REPOSITORY https://github.com/taglib/taglib.git
  GIT_TAG v1.10
  CMAKE_ARGS
    "-DCMAKE_SYSTEM_NAME=${CMAKE_SYSTEM_NAME}"
    "-DCMAKE_INSTALL_PREFIX=${TAGLIB_INSTALL_DIR}"
    "-DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}"
    "-DENABLE_STATIC=ON"
    "-DBUILD_SHARED_LIBS=OFF"
    "-DZLIB_LIBRARY=${ZLIB_STATIC_LIBRARY_LOCATION}"
    "-DZLIB_INCLUDE_DIR=${ZLIB_INSTALL_DIR}/include"
)
SET(TAGLIB_STATIC_LIBRARY_OUTPUT_NAME tag)
SET(TAGLIB_STATIC_LIBRARY_NAME "${CMAKE_STATIC_LIBRARY_PREFIX}${TAGLIB_STATIC_LIBRARY_OUTPUT_NAME}${CMAKE_STATIC_LIBRARY_SUFFIX}")
SET(TAGLIB_STATIC_LIBRARY_LOCATION "${TAGLIB_INSTALL_DIR}/lib/${TAGLIB_STATIC_LIBRARY_NAME}")

ADD_LIBRARY(${TAGLIB_STATIC_LIBRARY_OUTPUT_NAME} STATIC IMPORTED)

SET_TARGET_PROPERTIES(${TAGLIB_STATIC_LIBRARY_OUTPUT_NAME} PROPERTIES IMPORTED_LOCATION "${TAGLIB_STATIC_LIBRARY_LOCATION}")
# END taglib

INCLUDE_DIRECTORIES(
  "${PROJECT_BINARY_DIR}"
  "${ZLIB_INSTALL_DIR}/include"
  "${TAGLIB_INSTALL_DIR}/include/taglib"
)

ADD_EXECUTABLE(tagit tagit.cxx)
ADD_DEPENDENCIES(taglib zlib)
ADD_DEPENDENCIES(tagit taglib zlib)
TARGET_LINK_LIBRARIES(tagit ${TAGLIB_STATIC_LIBRARY_OUTPUT_NAME} ${ZLIB_STATIC_LIBRARY_OUTPUT_NAME})
